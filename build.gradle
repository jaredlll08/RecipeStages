buildscript {
    repositories {
        maven { url = 'https://maven.blamejared.com' }
        maven { url = 'https://maven.minecraftforge.net' }
        maven { url "https://plugins.gradle.org/m2/" }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath "gradle.plugin.com.matthewprenger:CurseGradle:1.4.0"
        classpath group: 'com.blamejared', name: 'ModTemplate', version: '2.+', changing: true
        classpath 'org.spongepowered:mixingradle:0.7.+'
        classpath "com.modrinth.minotaur:Minotaur:2.+"
    }
}

apply plugin: 'com.blamejared.modtemplate'
apply plugin: "com.matthewprenger.cursegradle"
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'maven-publish'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'com.modrinth.minotaur'

version = com.blamejared.modtemplate.Utils.updatingVersion('6.0.0')
group = "com.blamejared.recipestages"
archivesBaseName = "RecipeStages"

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

minecraft {
    mappings channel: 'official', version: '1.19.4'
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            workingDirectory project.file('run')

            mods {
                recipestages {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')

            mods {
                recipestages {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')

            args '--mod', 'recipestages', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')

            mods {
                recipestages {
                    source sourceSets.main
                }
            }
        }
    }
}

modTemplate {
    mcVersion "1.19.4"
    curseHomepage "https://www.curseforge.com/minecraft/mc-mods/recipe-stages"
    displayName "RecipeStages"
    modLoader "Forge"

    changelog {
        enabled true
        firstCommit "54fc92db82bd6ca67076ec9b57bc518d86daed53"
        repo "https://github.com/jaredlll08/RecipeStages"
    }
    versionTracker {
        enabled true
        projectName "RecipeStages"
        author "Jared"
    }
    webhook {
        enabled true
        curseId "280554"
        avatarUrl "https://media.forgecdn.net/avatars/128/69/636444415480522123.png"
    }
}

repositories {
    mavenCentral()
    maven {
        url 'https://maven.blamejared.com'
    }
    maven {
        url "https://dvs1.progwml6.com/files/maven"
    }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.19.4-45.1.4'
//
    implementation fg.deobf("com.blamejared.crafttweaker:CraftTweaker-forge-1.19.4:12.0.1")
//
    implementation fg.deobf("net.darkhax.gamestages:GameStages-Forge-1.19.4:13.0.1")
    implementation fg.deobf("net.darkhax.bookshelf:Bookshelf-Forge-1.19.4:18.0.6")

    compileOnly(fg.deobf("mezz.jei:jei-1.19.4-common-api:13.1.0.15"))
    compileOnly(fg.deobf("mezz.jei:jei-1.19.4-forge-api:13.1.0.15"))
    implementation(fg.deobf("mezz.jei:jei-1.19.4-forge:13.1.0.15"))
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : "recipestages",
                "Specification-Vendor"    : "BlameJared",
                "Specification-Version"   : "1",
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : "${version}",
                "Implementation-Vendor"   : "BlameJared",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}
jar.finalizedBy('reobfJar')

task sourcesJar(type: Jar, dependsOn: classes) {
    description = 'Creates a JAR containing the source code.'
    from sourceSets.main.allSource
    classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    description = 'Creates a JAR containing the JavaDocs.'
    from javadoc.destinationDir
    classifier = 'javadoc'
}

task deobfJar(type: Jar) {
    description = 'Creates a JAR containing the non-obfuscated compiled code.'
    from sourceSets.main.output
    classifier = "deobf"
}
artifacts {
    archives sourcesJar
    archives javadocJar
    archives deobfJar
}

publish.dependsOn(project.tasks.getByName("assemble"))
publish.mustRunAfter(project.tasks.getByName("build"))

publishing {

    publications {

        mavenJava(MavenPublication) {

            groupId project.group
            artifactId project.archivesBaseName
            version project.version
            from components.java

            // Allows the maven pom file to be modified.
            pom.withXml {

                // Go through all the dependencies.
                asNode().dependencies.dependency.each { dep ->

                    println 'Surpressing artifact ' + dep.artifactId.last().value().last() + ' from maven dependencies.'
                    assert dep.parent().remove(dep)
                }
            }

            artifact sourcesJar {

                classifier 'sources'
            }
            artifact javadocJar {

                classifier 'javadoc'
            }
            artifact deobfJar {

                classifier 'deobf'
            }
        }
    }

    repositories {

        maven {

            url "file://" + System.getenv("local_maven")
        }
    }
}

curseforge {

    apiKey = findProperty('curseforge_api_token') ?: 0
    project {
        id = "280554"
        releaseType = 'release'
        changelog = file("changelog.md")
        changelogType = 'markdown'
        addGameVersion '1.19.4'

        mainArtifact(jar) {
            relations {
                optionalDependency "crafttweaker"
                requiredDependency "game-stages"
            }
        }
        addArtifact(deobfJar)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}


tasks.withType(GenerateModuleMetadata) {

    enabled = false
}

modrinth {
    token = System.getenv("modrinth_token")
    projectId = "qXkLFB0L"
    changelog.set(file("changelog.md").text)
    versionName.set("Forge-1.19.4-$version")
    versionType = "release"
    uploadFile = jar
    dependencies {
        required.project "game-stages"
        optional.project "crafttweaker"
    }
}